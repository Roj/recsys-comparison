/* This file may be freely modified, used, and redistributed without restriction. */
/* Set up the repositories to get the LensKit plugin.
 * This configuration pulls in things needed for the build.gradle script itself */
buildscript {
    repositories {
        // LensKit snapshots are published to the Sonatype snapshot repository
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        // LensKit releases are published to Maven Central
        mavenCentral()
    }
    dependencies {
        classpath 'org.lenskit:lenskit-gradle:3.0-M2'
    }
}

/* Set up the repositories for getting LensKit and other libraries.
 * These repositories are used for your Java or Groovy code, and for running LensKit.
 */
repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenCentral()
}

dependencies {
    // Code needs to build with LensKit.
    compile "org.lenskit:lenskit-all:3.0-M2"
    // To run the code, we also need the LensKit CLI.
    runtime "org.lenskit:lenskit-cli:3.0-M2"

    // Tests use JUnit
    testCompile "junit:junit:4.12"
}

import org.lenskit.gradle.*

def dataset = 'amazon-digital-music'
def dataDir = 'data/'+dataset
def zipFile = "data/ratings_Digital_Music.zip"

/* Download the data set */
task fetchDataAmazonDigitalMusic {
    description 'Fetches the Amazon Digital Music data set.'

    doLast {
        mkdir dataDir
        ant {
            get(src: 'https://doc-0g-6s-docs.googleusercontent.com/docs/securesc/ha0ro937gcuc7l7deffksulhg5h7mbp1/78osragsd6ttfhst81emboee4nue5qnp/1487692800000/13305965733609396212/*/0B1Iw4oNXJh2AVm1hdC1jekcxa0E?e=download',
                    dest: zipFile,
                    skipExisting: true)
            unzip(src: zipFile, dest: dataDir) {
                patternset {
                    include name: '*'
                }
                mapper type: 'flatten'
            }
        }
    }
}

task crossfoldAmazonDigitalMusic (type: Crossfold, group: 'evaluate') {
    // download data before evaluating
    dependsOn fetchDataAmazonDigitalMusic

    input 'data/amazon-digital-music.yml'
    // test on random 1/5 of each user's ratings
    holdoutFraction(0.2, 'random')
    // use 5-fold cross-validation
    partitionCount 5
}

/* Run the LensKit evaluation */
task evaluateAmazonDigitalMusic(type: TrainTest, group: 'evaluate') {
    description 'Runs the Amazon Digital Music evaluation.'
    logFile "$buildDir/evaluate.log"
    logFileLevel 'DEBUG'

    // we add our crossfold task as evaluation input
    dataSet crossfoldAmazonDigitalMusic

    // send the output to appropriate files
    def datasetResultFolder = "$buildDir/results/"+dataset
    outputFile datasetResultFolder+"/eval-results.csv"
    userOutputFile datasetResultFolder+"/eval-users.csv"

    // configure our algorithms
    algorithm 'PersMean', 'algorithms/pers-mean.groovy'
    algorithm 'ItemItem', 'algorithms/item-item.groovy'
    algorithm 'UserUser', 'algorithms/user-user.groovy'
    algorithm 'FunkSVD', 'algorithms/funksvd.groovy'

    // and some evaluation tasks and metrics
    predict {
        metric 'rmse'
        metric 'ndcg'
    }
    recommend {
        listSize 10
        metric 'mrr'
    }
}

task cleanDataAmazonDigitalMusic {
    doLast {
        delete zipFile
        delete dataDir
    }
}