/* This file may be freely modified, used, and redistributed without restriction. */
/* Set up the repositories to get the LensKit plugin.
 * This configuration pulls in things needed for the build.gradle script itself */
buildscript {
    repositories {
        // LensKit snapshots are published to the Sonatype snapshot repository
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        // LensKit releases are published to Maven Central
        mavenCentral()
    }
    dependencies {
        classpath 'org.lenskit:lenskit-gradle:3.0-M2'
    }
}

/* Set up the repositories for getting LensKit and other libraries.
 * These repositories are used for your Java or Groovy code, and for running LensKit.
 */
repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenCentral()
}

dependencies {
    // Code needs to build with LensKit.
    compile "org.lenskit:lenskit-all:3.0-M2"
    // To run the code, we also need the LensKit CLI.
    runtime "org.lenskit:lenskit-cli:3.0-M2"

    // Tests use JUnit
    testCompile "junit:junit:4.12"
}

import org.lenskit.gradle.*

def dataset = 'bookcrossings'
def dataDir = 'data/'+dataset
def zipFile = "data/bookcrossings.zip"

/* Download the data set */
task fetchDataBookcrossings {
    description 'Fetches the Bookcrossing data set.'

    doLast {
        mkdir dataDir
        ant {
            get(src: 'http://drive.google.com/uc?export=download&id=0B2lSBow_51i-X25lSlNsS09wRTQ',
                    dest: zipFile,
                    skipExisting: true)
            unzip(src: zipFile, dest: dataDir) {
                patternset {
                    include name: '*'
                }
                mapper type: 'flatten'
            }
        }
    }
}

task crossfoldBookcrossings(type: Crossfold, group: 'evaluate') {
    // download data before evaluating
    dependsOn fetchDataBookcrossings

    input 'data/bookcrossings.yml'
    // test on random 1/5 of each user's ratings
    holdoutFraction(0.2, 'random')
    // use 5-fold cross-validation
    partitionCount 5
}

/* Run the LensKit evaluation */
task evaluateBookcrossings(type: TrainTest, group: 'evaluate') {
    description 'Runs the LensKit evaluation.'
    logFile "$buildDir/evaluate.log"
    logFileLevel 'DEBUG'

    // we add our crossfold task as evaluation input
    dataSet crossfoldBookcrossings

    // send the output to appropriate files
    def datasetResultFolder = "$buildDir/results/bookcrossings"
    outputFile datasetResultFolder+"/eval-results.csv"
    userOutputFile datasetResultFolder+"/eval-users.csv"

    // configure our algorithms
    algorithm 'PersMean', 'algorithms/pers-mean.groovy'
    algorithm 'ItemItem', 'algorithms/item-item.groovy'
    algorithm 'UserUser', 'algorithms/user-user.groovy'
    algorithm 'FunkSVD', 'algorithms/funksvd.groovy'

    // and some evaluation tasks and metrics
    predict {
        metric 'rmse'
        metric 'ndcg'
    }
    recommend {
        listSize 10
        metric 'mrr'
    }
}

task cleanDataBookcrossings{
    doLast {
        delete dataDir
        delete zipFile
    }
}